`default_nettype none
//-------------------------------------------------------------------------------------------------
module psd
//-------------------------------------------------------------------------------------------------
(
	input  wire       clock50,

	output wire[ 2:0] led,

	output wire[17:0] rgb,
	output wire[ 1:0] sync,

	input  wire       tape,

	output wire       i2sCk,
	output wire       i2sWs,
	output wire       i2sQ,

	input  wire       spiCk,
	input  wire       spiSs1,
	input  wire       spiSs2,
	input  wire       spiSs3,
	input  wire       spiMosi,
	output wire       spiMiso

	// output wire       dramCk,
	// output wire       dramCe,
	// output wire       dramCs,
	// output wire       dramWe,
	// output wire       dramRas,
	// output wire       dramCas,
	// output wire[ 1:0] dramDQM,
	// inout  wire[15:0] dramDQ,
	// output wire[ 1:0] dramBA,
	// output wire[11:0] dramA,
);
//--- clock ---------------------------------------------------------------------------------------

	wire clock0;
	wire power0;

	pll0 pll0(clock50, clock0, power0);

	wire clock1;
	wire power1;

	pll1 pll1(clock50, clock1, power1);

	wire clock = model ? clock1 : clock0;
	wire power = power1 && power0;

	reg[3:0] ce;
	always @(negedge clock, negedge power) if(!power) ce <= 1'd0; else ce <= ce+1'd1;

	wire ne14M = ce[1:0] == 3;
	wire ne7M0 = ce[2:0] == 7;
	wire pe7M0 = ce[2:0] == 3;

	wire ne3M5 = ce[3:0] == 15;
	wire pe3M5 = ce[3:0] == 7;
	
//--- mist ----------------------------------------------------------------------------------------

	wire r;
	wire g;
	wire b;
	wire i;
	wire hsync;
	wire vsync;

	wire ps2kCk;
	wire ps2kD;

	wire[7:0] joy1;
	wire[7:0] joy2;

	wire[2:0] mbtns;
	wire[7:0] xaxis;
	wire[7:0] yaxis;

	wire[63:0] status;

	wire       romIo;
	wire       tzxIo;
	wire[31:0] dioSz;
	wire[26:0] dioA;
	wire[ 7:0] dioD;
	wire       dioW;

	wire sdcCs;
	wire sdcCk;
	wire sdcMosi;
	wire sdcMiso;

	mist #(.RGBW(6)) mist
	(
		.clock  (clock  ),
		.ne14M  (ne14M  ),
		.ne7M0  (ne7M0  ),
		.spiCk  (spiCk  ),
		.spiSs1 (spiSs1 ),
		.spiSs2 (spiSs2 ),
		.spiSs3 (spiSs3 ),
		.spiMosi(spiMosi),
		.spiMiso(spiMiso),
		.status (status ),
		.r      (r      ),
		.g      (g      ),
		.b      (b      ),
		.i      (i      ),
		.hsync  (hsync  ),
		.vsync  (vsync  ),
		.rgb    (rgb    ),
		.sync   (sync   ),
		.ps2kCk (ps2kCk ),
		.ps2kD  (ps2kD  ),
		.joy1   (joy1   ),
		.joy2   (joy2   ),
		.mbtns  (mbtns  ),
		.xaxis  (xaxis  ),
		.yaxis  (yaxis  ),
		.romIo  (romIo  ),   
		.tzxIo  (tzxIo  ),   
		.dioSz  (dioSz  ),   
		.dioA   (dioA   ),  
		.dioD   (dioD   ),  
		.dioW   (dioW   ),  
		.sdcCs  (sdcCs  ),
		.sdcCk  (sdcCk  ),
		.sdcMosi(sdcMosi),
		.sdcMiso(sdcMiso)
	);

//--- audio ---------------------------------------------------------------------------------------

	wire[14:0] left;
	wire[14:0] right;

	wire[15:0] lmix = { 1'd0,  left }+{ 4'd0, {12{ !status[5] && ear}} };
	wire[15:0] rmix = { 1'd0, right }+{ 4'd0, {12{ !status[5] && ear}} };

	i2s i2s(clock50, lmix, rmix, i2sCk, i2sWs, i2sQ);
	
//--- keyboard ------------------------------------------------------------------------------------

	wire      strb;
	wire[7:0] code;

	ps2k ps2k(clock, ps2kCk, ps2kD, strb, code);

	wire[7:0] row;
	wire[4:0] col;
	wire F5;
	wire F9;
	wire play;
	wire stop;

	matrix matrix(clock, strb, code, 6'h3F, 6'h3F, row, col, ,,,, F5, F9, play, stop);

//--- memory --------------------------------------------------------------------------------------

	wire ready = 1'b1;
	wire mreq;
	wire rfsh;

	wire[13:0] a1;
	wire[ 7:0] q1;

	wire[18:0] a2;
	wire[ 7:0] d2;
	wire[ 7:0] q2 = a2[18:17] ? memQ : romQ;
	wire w2;
	wire r2;

	reg[2:0] romMap;
	always @(*) case(a2[16:14])
		default: romMap = 3'd0; // 48K
		 3'b010: romMap = 3'd1; // 128K.0
		 3'b011: romMap = 3'd2; // 128K.1
		 3'b100: romMap = 3'd3; // esxdos
	endcase

	wire[7:0] romQ;
	ram #(64) rom(clock, romIo ? dioA[15:0] : { romMap[1:0], a2[13:0] }, dioD, romQ, romIo && dioW);

	dprs #(16) dpr(clock, a1, q1, clock, { a2[15], a2[12:0] }, d2, !mreq && !w2 && a2[18:17] == 2 && a2[16] && a2[14] && !a2[13]);

	wire[7:0] memQ;
	ram #(256) mem(clock, a2[17:0], d2, memQ, !mreq && !w2 && a2[18:17]);

	/*
	wire[7:0] dprQ;
	dprf #(384) dpr
	(
		clock,
		{ 2'd2, 1'b1, a1[13], 2'b10, a1[12:0] },
		q1,
		clock,
		romIo ? dioA[18:0] : { a2[18:17], a2[18:17] == 0 ? romMap : a2[16:14], a2[13:0] },
		romIo ? dioD : d2,
		q2,
		romIo ? dioW : !mreq && !w2 && a2[18:17] != 0
	);
	*/

/*
	wire mreq;
	wire rfsh;

	wire romIo = dioEn && dioIx[5:0] == 0;

	reg r2p = 1'b1;
	always @(posedge clock) if(pe3M5) r2p <= r2;

	dprs #(16) drp(clock, a1, q1, clock, { a2[15], a2[12:0] }, d2, w1);

	wire[21:0] sdrA = romIo ? dioA[21:0] : { 3'd0, a2 };
	wire[15:0] sdrD = { 8'hFF, romIo ? dioD : d2 };
	wire[15:0] sdrQ;
	wire       sdrR = !mreq && !r2p;
	wire       sdrW = romIo ? dioW : !mreq && !w2 && a2[18:17];

	sdram sdram
	(
		.clock  (clock  ),
		.reset  (power  ),
		.ready  (ready  ),
		.rfsh   (rfsh   ),
		.a      (sdrA   ),
		.d      (sdrD   ),
		.q      (sdrQ   ),
		.rd     (sdrR   ),
		.wr     (sdrW   ),
		.dramCs (dramCs ),
		.dramRas(dramRas),
		.dramCas(dramCas),
		.dramWe (dramWe ),
		.dramDQM(dramDQM),
		.dramDQ (dramDQ ),
		.dramBA (dramBA ),
		.dramA  (dramA  )
	);

	assign dramCk = clock;
	assign dramCe = 1'b1;
*/

//--- tzx -----------------------------------------------------------------------------------------

	localparam TK = 256;
	localparam TW = $clog2(TK*1024);

	reg[TW-1:0] tzxSize;
	always @(posedge clock) if(tzxIo) tzxSize <= dioSz[TW-1:0];

	wire[TW-1:0] tzxA;
	wire[   7:0] tzxQ;

	ram #(TK) ram(clock, tzxIo ? dioA[TW-1:0] : tzxA, dioD, tzxQ, tzxIo && dioW);

	wire tzxBusy;
	wire tzxTape;

	tzx #(56000, TW) tzx
	(
		.clock  (clock  ),
		.ce     (1'b1   ),
		.a      (tzxA   ),
		.d      (tzxQ   ),
		.play   (!play  ),
		.stop   (!stop  ),
		.busy   (tzxBusy),
		.size   (tzxSize),
		.tape   (tzxTape)
	);

//--- zx ------------------------------------------------------------------------------------------

	reg rsp = 1'b0;
	reg rse = 1'b1;
	always @(posedge clock) begin rsp <= model; rse <= rsp == model; end

	wire model = status[3];
	wire divmmc = !status[4];

	wire reset = power && ready && F9 && !status[1] && !romIo && rse;
	wire nmi = F5 && !status[2];

	wire ear = tzxBusy ? tzxTape : ~tape;

	zx zx
	(
		.model  (model  ),
		.divmmc (divmmc ),
		.clock  (clock  ),
		.ne7M0  (ne7M0  ),
		.pe7M0  (pe7M0  ),
		.ne3M5  (ne3M5  ),
		.pe3M5  (pe3M5  ),
		.reset  (reset  ),
		.mreq   (mreq   ),
		.rfsh   (rfsh   ),
		.nmi    (nmi    ),
		.a1     (a1     ),
		.q1     (q1     ),
		.a2     (a2     ),
		.d2     (d2     ),
		.q2     (q2     ),
		.r2     (r2     ),
		.w2     (w2     ),
		.r      (r      ),
		.g      (g      ),
		.b      (b      ),
		.i      (i      ),
		.hsync  (hsync  ),
		.vsync  (vsync  ),
		.ear    (ear    ),
		.midi   (       ),
		.left   (left   ),
		.right  (right  ),
		.col    (col    ),
		.row    (row    ),
		.joy1   (joy1   ),
		.joy2   (joy2   ),
		.mbtns  (mbtns  ),
		.xaxis  (xaxis  ),
		.yaxis  (yaxis  ),
		.sdcCs  (sdcCs  ),
		.sdcCk  (sdcCk  ),
		.sdcMosi(sdcMosi),
		.sdcMiso(sdcMiso)
	);

//-------------------------------------------------------------------------------------------------

	assign led = { sdcCs, ~ear, ~ear };

//-------------------------------------------------------------------------------------------------
endmodule
//-------------------------------------------------------------------------------------------------
